%{        
int yydebug = 1;
%}

%define parse.trace

/* calculator with AST */
%language "c++"

/* write out a header file containing the token defines */
%defines

/* use newer C++ skeleton file */
%skeleton "lalr1.cc"
%define api.parser.class {Parser}
%parse-param { class Driver& driver }

%code requires{
#include "ast.hpp"
}


%code top {

#include <memory>
#include <stdio.h>
#include <stdlib.h>
#include <iostream>
#include "lexer.h"
#include "declarations.hpp"
#include "driver.h"
#undef yylex
#define yylex driver.lexer->lex

}


%start start
%define parse.error verbose

%locations 
%initial-action
{
    @$.begin.filename = @$.end.filename = &driver.streamname;
};

%union {
        ASTNodeBase *a;
}
/* declare tokens */

%token END 0

%token <a> ID
%token <a> NUMBER
%token <a> STRING_LITERAL
%token <a> TRUE
%token <a> FALSE
%token <a> BOOLEAN
%token <a> INT

%left <a> PLUS
%left <a> MINUS
%left <a> MULTIPLY
%left <a> DIVIDE
%left <a> MODULO

%left <a> AND
%left <a> OR

%token <a> IF
%token <a> ELSE
%token <a> WHILE
%token <a> VOID
%token <a> RETURN
%token <a> BREAK

%right <a> ASSIGNMENT
%right <a> NOT
%token <a> LT
%token <a> GT
%token <a> GE
%token <a> LE
%token <a> EQ
%token <a> NE

%expect 1

%token <a> PARENTHESIS_OPEN
%token <a> PARENTHESIS_CLOSE
%token <a> BRACE_OPEN
%token <a> BRACE_CLOSE
%token <a> SEMICOLON
%token <a> COMMA

%type <a> globaldeclarations
%type <a> globaldeclaration
%type <a> variabledeclaration
%type <a> identifier
%type <a> functiondeclaration
%type <a> functionheader
%type <a> functiondeclarator
%type <a> formalparameterlist
%type <a> formalparameter
%type <a> mainfunctiondeclaration
%type <a> mainfunctiondeclarator
%type <a> block
%type <a> blockstatements
%type <a> blockstatement
%type <a> statement
%type <a> statementexpression
%type <a> primary
%type <a> argumentlist
%type <a> functioninvocation
%type <a> postfixexpression
%type <a> unaryexpression
%type <a> multiplicativeexpression
%type <a> additiveexpression
%type <a> relationalexpression
%type <a> equalityexpression
%type <a> conditionalandexpression
%type <a> conditionalorexpression
%type <a> assignmentexpression
%type <a> assignment
%type <a> expression
%type <a> literal
%type <a> type
%type <a> start

%%

start   : /* nothing */ { driver.root = std::make_unique<ASTNode<>>(ASTNodeType::start, "code");
}
        | globaldeclarations { driver.root =
        std::make_unique<ASTNode<>>(ASTNodeType::start, "code",
        std::initializer_list<ASTNodeBase*>{$1}); }
        ;

literal : NUMBER { $$->set_attribute("location", @$);}
        | STRING_LITERAL { $$->set_attribute("location", @$);}
        | TRUE { $$->set_attribute("location", @$);}
        | FALSE { $$->set_attribute("location", @$);}

type    : BOOLEAN { $$->set_attribute("location", @$);}
        | INT { $$->set_attribute("location", @$);}
        ;

globaldeclarations      : globaldeclaration { $$ = new ASTNode(ASTNodeType::globaldeclarations, "global declarations", {$1}); $$->set_attribute("location", @$); }
                        | globaldeclarations globaldeclaration { $$->add($2); $$->set_attribute("location", @$); }
                        ;

globaldeclaration       : variabledeclaration 
                        | functiondeclaration 
                        | mainfunctiondeclaration
                        
                        ;

variabledeclaration     : type identifier SEMICOLON { $$ = new ASTNode(ASTNodeType::variabledeclaration, "variable declaration", {$1, $2}); $$->set_attribute("location", @$); }
                        ;

identifier              : ID { $$ = $1; $$->set_attribute("location", @$); $$->set_attribute("location", @$); }
                        ;

functiondeclaration     : functionheader block 
                        { $$ = new ASTNode(ASTNodeType::functiondeclaration, "function declaration", {$1, $2}); $$->set_attribute("location", @$); }
                        ;

functionheader          : type functiondeclarator { $$ = new ASTNode(ASTNodeType::functionheader, "function header", {$1, $2}); $$->set_attribute("location", @$); }

                        | VOID functiondeclarator { $$ = new ASTNode(ASTNodeType::functionheader, "function header", {$1, $2}); $$->set_attribute("location", @$); }
                        ;

functiondeclarator      : identifier PARENTHESIS_OPEN formalparameterlist PARENTHESIS_CLOSE { $$ = new ASTNode(ASTNodeType::functiondeclarator, "function declarator", {$1, $3}); $$->set_attribute("location", @$); }
                        | identifier PARENTHESIS_OPEN PARENTHESIS_CLOSE { $$ = new ASTNode(ASTNodeType::functiondeclarator, "function declarator", {$1}); $$->set_attribute("location", @$); }
                        ;

formalparameterlist     : formalparameter  { $$ = new ASTNode(ASTNodeType::formalparameterlist, "formal parameter list", {$1}); $$->set_attribute("location", @$); }
                        | formalparameterlist COMMA formalparameter { $$->add($3); $$->set_attribute("location", @$); }
                        ;

formalparameter         : type identifier  { $$ = new ASTNode(ASTNodeType::formalparameter, "formal parameter", {$1, $2}); $$->set_attribute("location", @$); }
                        ;

mainfunctiondeclaration : mainfunctiondeclarator block { $$ = new ASTNode(ASTNodeType::mainfunctiondeclaration, "main function declaration", {$1, $2}); $$->set_attribute("location", @$); }
                        ;

mainfunctiondeclarator  : identifier PARENTHESIS_OPEN PARENTHESIS_CLOSE { $$ = new ASTNode(ASTNodeType::mainfunctiondeclarator, "function declarator", {$1}); $$->set_attribute("location", @$); }
                        | identifier PARENTHESIS_OPEN formalparameterlist PARENTHESIS_CLOSE { $$ = new ASTNode(ASTNodeType::mainfunctiondeclarator, "function declarator", {$1, $3}); $$->set_attribute("location", @$); }
                        ;

block                   : BRACE_OPEN blockstatements BRACE_CLOSE { $$ = new ASTNode(ASTNodeType::block, "block", {$2}); $$->set_attribute("location", @$); }
                        | BRACE_OPEN BRACE_CLOSE { $$ = new ASTNode(ASTNodeType::block, "block"); $$->set_attribute("location", @$); }
                        ;

blockstatements         : blockstatement { $$ = new ASTNode(ASTNodeType::blockstatements, "block statements", {$1}); $$->set_attribute("location", @$); }
                        | blockstatements blockstatement { $$->add($2); $$->set_attribute("location", @$); }
                        ;

blockstatement          : variabledeclaration
                        | statement
                        ;

statement               : block
                        | SEMICOLON { $$ = new ASTNode(ASTNodeType::statement, "empty statement"); $$->set_attribute("location", @$); $$->set_attribute("location", @$); }
                        | statementexpression SEMICOLON { $$ = $1; $$->set_attribute("location", @$); $$->set_attribute("location", @$); }
                        | BREAK SEMICOLON { $$ = new ASTNode(ASTNodeType::breakstatement, "break"); $$->set_attribute("location", @$); }
                        | RETURN expression SEMICOLON { $$ = new ASTNode(ASTNodeType::returnstatement, "return", {$2}); $$->set_attribute("location", @$); }
                        | RETURN SEMICOLON { $$ = new ASTNode(ASTNodeType::returnstatement, "return"); $$->set_attribute("location", @$); }
                        | IF PARENTHESIS_OPEN expression PARENTHESIS_CLOSE statement { $$ = new ASTNode(ASTNodeType::ifstatement, "if", {$3,$5}); $$->set_attribute("location", @$); }
                        | IF PARENTHESIS_OPEN expression PARENTHESIS_CLOSE statement ELSE statement { $$ = new ASTNode(ASTNodeType::ifstatement, "if else", {$3,$5,$7}); $$->set_attribute("location", @$); }
                        | WHILE PARENTHESIS_OPEN expression PARENTHESIS_CLOSE statement { $$ = new ASTNode(ASTNodeType::whilestatement, "while", {$3,$5}); $$->set_attribute("location", @$); }
                        ;

statementexpression     : assignment { $$ = new ASTNode(ASTNodeType::statementexpression, "statement expression", {$1}); $$->set_attribute("location", @$); }
                        | functioninvocation { $$ = new ASTNode(ASTNodeType::statementexpression, "statement expression", {$1}); $$->set_attribute("location", @$); }
                        ;

primary                 : literal
                        | PARENTHESIS_OPEN expression PARENTHESIS_CLOSE { $$ = $2; }
                        | functioninvocation
                        ;

argumentlist            : expression { $$ = new ASTNode(ASTNodeType::argumentlist, "argument list", {$1}); $$->set_attribute("location", @$); }
                        | argumentlist COMMA expression { $$->add($3); $$->set_attribute("location", @$); }
                        ;

functioninvocation      : identifier PARENTHESIS_OPEN argumentlist PARENTHESIS_CLOSE { $$ = new ASTNode(ASTNodeType::functioninvocation, "function invocation", {$1,$3}); $$->set_attribute("location", @$); }
                        | identifier PARENTHESIS_OPEN PARENTHESIS_CLOSE { $$ = new ASTNode(ASTNodeType::functioninvocation, "function invocation", {$1}); $$->set_attribute("location", @$); }
                        ;

postfixexpression       : primary
                        | identifier
                        ;

unaryexpression         : MINUS unaryexpression { $$ = new ASTNode(ASTNodeType::unaryexpression, "-", {$2}); $$->set_attribute("location", @$); }
                        | NOT unaryexpression { $$ = new ASTNode(ASTNodeType::unaryexpression, "!", {$2}); $$->set_attribute("location", @$); }
                        | postfixexpression
                        ;

multiplicativeexpression: unaryexpression
                        | multiplicativeexpression MULTIPLY unaryexpression { $$ = new ASTNode(ASTNodeType::infixoperator, "*", {$1,$3}); $$->set_attribute("location", @$); }
                        | multiplicativeexpression DIVIDE unaryexpression { $$ = new ASTNode(ASTNodeType::infixoperator, "/", {$1,$3}); $$->set_attribute("location", @$); }
                        | multiplicativeexpression MODULO unaryexpression { $$ = new ASTNode(ASTNodeType::infixoperator, "%", {$1,$3}); $$->set_attribute("location", @$); }
                        ;

additiveexpression      : multiplicativeexpression
                        | additiveexpression PLUS multiplicativeexpression { $$ = new ASTNode(ASTNodeType::infixoperator, "+", {$1,$3}); $$->set_attribute("location", @$); }
                        | additiveexpression MINUS multiplicativeexpression { $$ = new ASTNode(ASTNodeType::infixoperator, "-", {$1,$3}); $$->set_attribute("location", @$); }
                        ;

relationalexpression    : additiveexpression
                        | relationalexpression LT additiveexpression { $$ = new ASTNode(ASTNodeType::infixoperator, "<", {$1,$3}); $$->set_attribute("location", @$); }
                        | relationalexpression GT additiveexpression { $$ = new ASTNode(ASTNodeType::infixoperator, ">", {$1,$3}); $$->set_attribute("location", @$); }
                        | relationalexpression LE additiveexpression { $$ = new ASTNode(ASTNodeType::infixoperator, "<=", {$1,$3}); $$->set_attribute("location", @$); }
                        | relationalexpression GE additiveexpression { $$ = new ASTNode(ASTNodeType::infixoperator, ">=", {$1,$3}); $$->set_attribute("location", @$); }
                        ;

equalityexpression      : relationalexpression
                        | equalityexpression EQ relationalexpression { $$ = new ASTNode(ASTNodeType::infixoperator, "==", {$1,$3}); $$->set_attribute("location", @$); }
                        | equalityexpression NE relationalexpression { $$ = new ASTNode(ASTNodeType::infixoperator, "!=", {$1,$3}); $$->set_attribute("location", @$); }
                        ;

conditionalandexpression: equalityexpression
                        | conditionalandexpression AND equalityexpression { $$ = new ASTNode(ASTNodeType::infixoperator, "&&", {$1,$3}); $$->set_attribute("location", @$); }
                        ;

conditionalorexpression : conditionalandexpression
                        | conditionalorexpression OR conditionalandexpression { $$ = new ASTNode(ASTNodeType::infixoperator, "||", {$1,$3}); $$->set_attribute("location", @$); }
                        ;

assignmentexpression    : conditionalorexpression
                        | assignment
                        ;

assignment              : identifier ASSIGNMENT assignmentexpression { $$ = new ASTNode(ASTNodeType::infixoperator, "=", {$1,$3}); $$->set_attribute("location", @$); }
                        ;

expression              : assignmentexpression
                        ;

%%

void yy::Parser::error(const location_type& l, const std::string& m)
{
    driver.error(l, m);
}